<INICIO> ::= <INSTRUCCIONES>

<INSTRUCCIONES> ::= <INSTRUCCIONES> <INSTRUCCION> | <INSTRUCCION>

<INSTRUCCION> ::= <IMPRESION>
                | <DECLARACION>
                | <ASIGNACION>
                | <IIF>
                | <IFOR>
                | <BREAKK>
                | <CONTINUEE>
                | <INCREMENTO> "FINCADENA"
                | <DECREMENTO> "FINCADENA"
                | <IWHILE>
                | <IDOWHILE>
                | <IMATCH>
                | <METODO>
                | <ISTARTWITH>
                | <LLAMADA>
                | <DECLARACIONVECTORES>
                | <IRETURN>
                | <DECLARACIONLISTA>
                | <IAPPEND>
                | <IREMOVE>
                | error "FINCADENA"

<IMPRESION> ::= "IMPRIMIR" "(" <EXPRESION> ")" "FINCADENA"

<DECLARACION> ::= "MUTABILIDAD" "ID" ":" <TIPOS> "=" <EXPRESION> "FINCADENA"
                | "MUTABILIDAD" "ID" ":" <TIPOS> "FINCADENA"

<ASIGNACION> ::= "ID" "=" <EXPRESION> "FINCADENA"
               | "ID" "[" <EXPRESION> "]" "=" <EXPRESION> "FINCADENA"
               | "ID" "[" <EXPRESION> "]" "[" <EXPRESION> "]" "=" <EXPRESION> "FINCADENA"

<METODO> ::= <TIPOS> "ID" "(" <PARAMS> ")" "{" <INSTRUCCIONES> "}"
           | <TIPOS> "ID" "(" ")" "{" <INSTRUCCIONES> "}"

<LLAMADA> ::= "ID" "(" <PARAMSCALL> ")" "FINCADENA"
            | "ID" "(" ")" "FINCADENA"

<PARAMS> ::= <PARAMS> "," <TIPOS> "ID"
           | <TIPOS> "ID"

<ISTARTWITH> ::= "STARTWITH" "ID" "(" <PARAMSCALL> ")" "FINCADENA"
               | "STARTWITH" "ID" "(" ")" "FINCADENA"

<PARAMSCALL> ::= <PARAMSCALL> "," <EXPRESION>
               | <EXPRESION>

<DECLARACIONVECTORES> ::= "MUTABILIDAD" "ID" ":" <TIPOS> "[]" "=" "{" <VECTORESLISTA> "}" "FINCADENA"
                         | "MUTABILIDAD" "ID" ":" <TIPOS> "[][]" "=" "{" <VECTORESLISTADOSDIMENSIONES> "}" "FINCADENA"

<VECTORESLISTA> ::= <VECTORESLISTA> "," <EXPRESION>
                  | <EXPRESION>

<VECTORESLISTADOSDIMENSIONES> ::= <VECTORESLISTADOSDIMENSIONES> "," "{" <VECTORESLISTA> "}"
                                | "{" <VECTORESLISTA> "}"

<DECLARACIONLISTA> ::= "LIST" "<" <TIPOS> ">" "ID" "=" "NEW" "LIST" "(" ")" "FINCADENA"

<IAPPEND> ::= "ID" "." "APPEND" "(" <EXPRESION> ")" "FINCADENA"

<IIF> ::= "IF" "(" <EXPRESION> ")" "{" <INSTRUCCIONES> "}"
        | "IF" "(" <EXPRESION> ")" "{" <INSTRUCCIONES> "}" "ELSE" "{" <INSTRUCCIONES> "}"
        | "IF" "(" <EXPRESION> ")" "{" <INSTRUCCIONES> "}" "ELSE" <IIF>

<IFOR> ::= "FOR" "(" <ASIGNACION> <EXPRESION> "FINCADENA" <ACTFOR> ")" "{" <INSTRUCCIONES> "}"

<ACTFOR> ::= "ID" "=" <EXPRESION>
           | <INCREMENTO>
           | <DECREMENTO>

<BREAKK> ::= "BREAK" "FINCADENA"

<CONTINUEE> ::= "CONTINUE" "FINCADENA"

<IRETURN> ::= "RETURN" <EXPRESION> "FINCADENA"
            | "RETURN" "FINCADENA"

<INCREMENTO> ::= "ID" "++"

<DECREMENTO> ::= "ID" "--"

<TIPOS> ::= "INT"
          | "DOUBLE"
          | "STRING"
          | "CHAR"
          | "BOOL"
          | "VOID"

<IWHILE> ::= "WHILE" "(" <EXPRESION> ")" "{" <INSTRUCCIONES> "}"

<IDOWHILE> ::= "DO" "{" <INSTRUCCIONES> "}" "WHILE" "(" <EXPRESION> ")" "FINCADENA"

<IMATCH> ::= "MATCH" <EXPRESION> "{" <CASOS> <DEFAULT> "}"
           | "MATCH" <EXPRESION> "{" <CASOS> "}"
           | "MATCH" <EXPRESION> "{" <DEFAULT> "}"

<CASOS> ::= <CASOS> <CASO>
          | <CASO>

<CASO> ::= <EXPRESION> "=>" "{" <INSTRUCCIONES> "}"

<DEFAULT> ::= "_" "=>" "{" <INSTRUCCIONES> "}"

<EXPRESION> ::= "-" <EXPRESION>
              | <EXPRESION> "+" <EXPRESION>
              | <EXPRESION> "-" <EXPRESION>
              | <EXPRESION> "*" <EXPRESION>
              | <EXPRESION> "/" <EXPRESION>
              | <EXPRESION> "^" <EXPRESION>
              | <EXPRESION> "%" <EXPRESION>
              | <EXPRESION> "==" <EXPRESION>
              | <EXPRESION> "<" <EXPRESION>
              | <EXPRESION> ">" <EXPRESION>
              | <EXPRESION> "<=" <EXPRESION>
              | <EXPRESION> ">=" <EXPRESION>
              | <EXPRESION> "!=" <EXPRESION>
              | <EXPRESION> "||" <EXPRESION>
              | <EXPRESION> "&&" <EXPRESION>
              | <EXPRESION> "^^" <EXPRESION>
              | <LLAMADA>
              | "!" <EXPRESION>
              | "(" <TIPOS> ")" <EXPRESION>
              | "ENTERO"
              | "DECIMAL"
              | "CADENA"
              | "BOOLEANO"
              | "CARACTER"
              | "ID"
              | "ID" "[" <EXPRESION> "]"
              | "ID" "[" <EXPRESION> "]" "[" <EXPRESION> "]"
              | "ID" "." "REMOVE" "(" <EXPRESION> ")"
              | "ROUND" "(" <EXPRESION> ")"
              | "LENGTH" "(" <EXPRESION> ")"
              | "TOSTRING" "(" <EXPRESION> ")"
              | "ID" "." "FIND" "(" <EXPRESION> ")"
              | "(" <EXPRESION> ")"
