package analizadores;

//importaciones
import java_cup.runtime.Symbol;
import java.util.LinkedList;
import abstracto.Instruccion;
import abstracto.Casos;
import simbolo.Tipo;
import simbolo.tipoDato;
import excepciones.Errores;
import java.util.HashMap;

//instrucciones
import instrucciones.Print;
import instrucciones.Declaracion;
import instrucciones.AsignacionVar;
import instrucciones.If;
import instrucciones.For;
import instrucciones.Break;
import instrucciones.Continue;
import instrucciones.Incremento;
import instrucciones.Decremento;
import instrucciones.While;
import instrucciones.DoWhile;
import instrucciones.Match;
import instrucciones.Metodo;
import instrucciones.StartWith;
import instrucciones.Llamada;
import instrucciones.DeclaracionVectores;

//expresiones
import expresiones.Nativo;
import expresiones.Aritmeticas;
import expresiones.OperadoresAritmeticos;
import expresiones.Relacionales;
import expresiones.OperadoresRelacionales;
import expresiones.Logicos;
import expresiones.OperadoresLogicos;
import expresiones.AccesoVar;
import expresiones.Casteos;
import expresiones.Return;
import expresiones.AccesoVec;

// parser
parser code
{:
    scanner s;
    parser(scanner s){this.s = s;}

    public static LinkedList<Errores> listaErrores = new LinkedList<>();
    public static boolean elseinstr = true;

    
    public void syntax_error(Symbol s){
        listaErrores.add(new Errores("Error Sintactico Recuperable", "No se esperaba el componente "+ s.value , s.left, s.right));
        System.out.println("Error Sintactico en la linea " +
        (s.left) + " y columna " + (s.right) +
        ". No se esperaba el componente: " + (s.value) + ".");
    }

    public void unrecovered_syntax_error (Symbol s){
        listaErrores.add(new Errores("Error Sintactico no Recuperable", "No se esperaba el componente "+ s.value , s.left, s.right));
        System.out.println("Error Sintactico no recuperable en la linea" +
        (s.left) + " y columna " + (s.right) +
        ". No se esperaba el componente: " + (s.value) + ".");
    }

:}

action code
{:


:}

//terminales
terminal String CADENA, ENTERO, DECIMAL, BOOLEANO, CARACTER, ID,MUTABILIDAD;
terminal FINCADENA, IMPRIMIR,INT,DOUBLE,STRING,CHAR,BOOL,DOSPUNTOS,IF,ELSE,FOR;
terminal PAR1, PAR2, MAS, MENOS, UMENOS, POTENCIA, MULTIPLICACION, DIVISION, MODULO, LLAVE1,LLAVE2;
terminal IGUALACION, MENORQUE, MAYORQUE, MENOR, MAYOR, DIFERENTE,IGUAL,FLECHA,BARRABAJA,COMA;
terminal OR, AND, NOT, XOR;
terminal String BREAK,CONTINUE,WHILE,DO, MATCH;
terminal VOID, STARTWITH;
terminal CORCHE1,CORCHE2,RETURN;


//no terminales
nonterminal LinkedList<Instruccion> INICIO, INSTRUCCIONES;
nonterminal LinkedList<HashMap> PARAMS;
nonterminal Instruccion ISTARTWITH;
nonterminal LinkedList<Instruccion> PARAMSCALL;
nonterminal LinkedList<Casos> CASOS;
nonterminal Instruccion INSTRUCCION;
nonterminal Instruccion IMPRESION,DECLARACION,ASIGNACION,IIF,IFOR,ACTFOR,IWHILE,IDOWHILE;
nonterminal Instruccion BREAKK,CONTINUEE,INCREMENTO,DECREMENTO,IMATCH;
nonterminal Casos CASO,DEFAULT;
nonterminal Tipo TIPOS;
nonterminal Instruccion EXPRESION;
nonterminal Instruccion METODO;
nonterminal Instruccion LLAMADA;
nonterminal Instruccion DECLARACIONVECTORES;
nonterminal LinkedList<Instruccion> VECTORESLISTA;
nonterminal LinkedList<LinkedList<Instruccion>> VECTORESLISTADOSDIMENSIONES;
nonterminal Instruccion IRETURN;



precedence left OR;
precedence left AND;
precedence left XOR;
precedence right NOT;
precedence left IGUALACION, MENORQUE, MAYORQUE, MENOR, MAYOR, DIFERENTE;
precedence left MAS, MENOS;
precedence left MULTIPLICACION, DIVISION, MODULO;
precedence left POTENCIA;
precedence left UMENOS;



start with INICIO;

INICIO ::= INSTRUCCIONES:a      {:   RESULT = a;   :}
;

INSTRUCCIONES ::= INSTRUCCIONES:a INSTRUCCION:b     {: RESULT = a; RESULT.add(b); :}
                | INSTRUCCION:a                   {: RESULT = new LinkedList<>(); RESULT.add(a); :}
;

INSTRUCCION ::=  IMPRESION:a                    {:  RESULT = a; :}
                | DECLARACION:a                   {: RESULT = a; :}
                | ASIGNACION:a                   {: RESULT = a; :}
                | IIF:a                          {: RESULT = a; :} 
                | IFOR:a                         {: RESULT = a; :}
                | BREAKK:a                       {: RESULT = a; :}
                | CONTINUEE:a                    {: RESULT = a; :}
                | INCREMENTO:a FINCADENA         {: RESULT = a; :}
                | DECREMENTO:a FINCADENA         {: RESULT = a; :}
                | IWHILE:a                      {: RESULT = a; :}
                | IDOWHILE:a                    {: RESULT = a; :}
                | IMATCH:a                      {: RESULT = a; :}
                | METODO:a                      {: RESULT = a; :}
                | ISTARTWITH:a                    {: RESULT = a; :}
                | LLAMADA:a                     {: RESULT = a; :}
                | DECLARACIONVECTORES:a         {: RESULT = a; :}
                | IRETURN:a                     {: RESULT = a; :}
                | error FINCADENA {: :}
;
IMPRESION ::= IMPRIMIR PAR1 EXPRESION:a PAR2 FINCADENA      {:   RESULT = new Print(a, aleft, aright);   :}
;

DECLARACION ::= MUTABILIDAD:d ID:b DOSPUNTOS TIPOS:a IGUAL EXPRESION:c FINCADENA {:  RESULT = new Declaracion(b, c, a,d, aleft, aright); :}
                | MUTABILIDAD:d ID:b DOSPUNTOS TIPOS:a FINCADENA {:  
                    Nativo c = new Nativo(tipoDato.NULL, a, aleft, aright);
                RESULT = new Declaracion(b,c, a,d, aleft, aright); :}
;
ASIGNACION ::= ID:a IGUAL EXPRESION:b FINCADENA  {:   RESULT=new AsignacionVar(a, b, aleft, aright);  :}
;



METODO ::= TIPOS:a ID:b PAR1 PARAMS:p PAR2 LLAVE1 INSTRUCCIONES:c LLAVE2    {:   RESULT= new Metodo(b, p, c, a, aleft, aright );   :}
        |  TIPOS:a ID:b PAR1 PAR2 LLAVE1 INSTRUCCIONES:c LLAVE2             {:   RESULT= new Metodo(b, new LinkedList<HashMap>(), c, a, aleft, aright );   :}
;


LLAMADA ::= ID:a PAR1 PARAMSCALL:b PAR2 FINCADENA      {:  RESULT= new Llamada(a, b, aleft, aright);    :}
            | ID:a PAR1 PAR2 FINCADENA                   {:  RESULT= new Llamada(a, new LinkedList<Instruccion>(), aleft, aright);    :}    
;

PARAMS ::= PARAMS:a COMA TIPOS:b ID:c   {:  
                                            RESULT = a;
                                            var hm2= new HashMap<String, Object>();
                                            hm2.put("id", c);
                                            hm2.put("tipo", b);
                                            RESULT.add(hm2);
                                        :}
        | TIPOS:a ID:b                  {:  RESULT = new LinkedList<HashMap>(); 
                                            var hm = new HashMap<String, Object>();
                                            hm.put("id",b);
                                            hm.put("tipo", a);
                                            RESULT.add(hm);
                                        :}
;

ISTARTWITH ::=  STARTWITH ID:a PAR1 PARAMSCALL:b PAR2 FINCADENA     {:   RESULT = new StartWith(a, b, aleft, aright );   :}
            | STARTWITH ID:a PAR1 PAR2 FINCADENA                  {:   RESULT = new StartWith(a, new LinkedList<Instruccion>(), aleft, aright );   :}
;

PARAMSCALL ::= PARAMSCALL:a COMA EXPRESION:e    {:  RESULT = a;
                                                    RESULT.add(e);
                                                :}
            | EXPRESION:a                       {:  RESULT = new LinkedList<Instruccion>(); 
                                                    RESULT.add(a);
                                                :}
;

DECLARACIONVECTORES ::= MUTABILIDAD:a ID:b DOSPUNTOS TIPOS:c CORCHE1 CORCHE2 IGUAL CORCHE1 VECTORESLISTA:d CORCHE2 FINCADENA 
                                        {:
                                        RESULT = new DeclaracionVectores(a, b, c, d,null, 1,aleft, aright); :}
                    | MUTABILIDAD:a ID:b DOSPUNTOS TIPOS:c CORCHE1 CORCHE2 CORCHE1 CORCHE2 IGUAL CORCHE1 VECTORESLISTADOSDIMENSIONES:d CORCHE2 FINCADENA 
                                        {:
                                        RESULT = new DeclaracionVectores(a, b, c,null, d, 2,aleft, aright);
                                        :}
;

VECTORESLISTA ::= VECTORESLISTA:a COMA EXPRESION:b {: RESULT = a; RESULT.add(b); :}
                | EXPRESION:a {: RESULT = new LinkedList<Instruccion>(); RESULT.add(a); :}
;

VECTORESLISTADOSDIMENSIONES ::= VECTORESLISTADOSDIMENSIONES:a COMA CORCHE1 VECTORESLISTA:b CORCHE2 {: RESULT = a; RESULT.add(b); :}
                            | CORCHE1 VECTORESLISTA:a CORCHE2 
                                    {: 
                                    RESULT = new LinkedList<LinkedList<Instruccion>>(); LinkedList<Instruccion> aux = new LinkedList<>(); aux = a; RESULT.add(aux); :}
;

IIF ::= IF PAR1 EXPRESION:a PAR2 LLAVE1 INSTRUCCIONES:b LLAVE2 {:
                                RESULT = new If(a,b,null,null, aleft, aright);
                                :}
        | IF PAR1 EXPRESION:a PAR2 LLAVE1 INSTRUCCIONES:b LLAVE2 ELSE LLAVE1 INSTRUCCIONES:c LLAVE2 {:
                                RESULT = new If(a,b,c,null, aleft, aright);
                                :}
        |IF PAR1 EXPRESION:a PAR2 LLAVE1 INSTRUCCIONES:b LLAVE2 ELSE IIF:c {:
                                RESULT = new If(a,b,null,c, aleft, aright);
                                :}
;




IFOR ::= FOR PAR1 ASIGNACION:a EXPRESION:b FINCADENA ACTFOR:c PAR2 LLAVE1 INSTRUCCIONES:d LLAVE2 
            {: RESULT = new For(a,b,c,d,aleft,aright);  :}
;
ACTFOR ::= ID:a IGUAL EXPRESION:b {: RESULT = new AsignacionVar(a, b, aleft, aright); :}
        | INCREMENTO:a {: RESULT = a; :}
        | DECREMENTO:a {: RESULT = a; :}
;
BREAKK ::= BREAK:a FINCADENA {: RESULT = new Break(aleft, aright); :}
;
CONTINUEE ::= CONTINUE:a FINCADENA {: RESULT = new Continue(aleft, aright); :}
;

IRETURN ::= RETURN EXPRESION:a FINCADENA {: RESULT = new Return(a, aleft, aright); :}
        | RETURN FINCADENA {: 


                            RESULT = new Return(null, 0, 0); 
                            :}
;       


INCREMENTO ::= ID:a MAS MAS {: RESULT = new Incremento(a,aleft,aright);  :}
;
DECREMENTO ::= ID:a MENOS MENOS {: RESULT = new Decremento(a,aleft,aright);  :}
;
TIPOS ::= INT {: RESULT = new Tipo(tipoDato.ENTERO); :}
        | DOUBLE {: RESULT = new Tipo(tipoDato.DECIMAL); :}
        | STRING {: RESULT = new Tipo(tipoDato.CADENA); :}
        | CHAR {: RESULT = new Tipo(tipoDato.CARACTER); :}
        | BOOL {: RESULT = new Tipo(tipoDato.BOOLEANO); :}
        | VOID {: RESULT = new Tipo(tipoDato.VOID); :}
;
IWHILE ::= WHILE PAR1 EXPRESION:a PAR2 LLAVE1 INSTRUCCIONES:b LLAVE2 {: RESULT = new While(a, b, aleft, aright); :}
;

IDOWHILE ::= DO LLAVE1 INSTRUCCIONES:a LLAVE2 WHILE PAR1 EXPRESION:b PAR2 FINCADENA {: RESULT = new DoWhile(b, a, aleft, aright); :}
;

IMATCH ::= MATCH EXPRESION:a LLAVE1 CASOS:b DEFAULT:c LLAVE2 {: RESULT = new Match(a, b, c, aleft, aright); :}
        | MATCH EXPRESION:a LLAVE1 CASOS:b LLAVE2 {: RESULT = new Match(a, b, null, aleft, aright); :}
        | MATCH EXPRESION:a LLAVE1 DEFAULT:c LLAVE2 {: RESULT = new Match(a, null, c, aleft, aright); :}
;

CASOS ::= CASOS:a CASO:b {: RESULT = a; RESULT.add(b); :}
        | CASO:a {: RESULT = new LinkedList<>(); RESULT.add(a); :}
;

CASO ::= EXPRESION:a FLECHA LLAVE1 INSTRUCCIONES:b LLAVE2 {: RESULT = new Casos(a, b, false, aleft, aright); :}
;

DEFAULT ::= BARRABAJA FLECHA LLAVE1 INSTRUCCIONES:a LLAVE2 {: RESULT = new Casos(null, a, true, aleft, aright); :}
;


EXPRESION ::= MENOS EXPRESION:a {:   RESULT = new Aritmeticas(a, OperadoresAritmeticos.NEGACION, aleft, aright);   :} %prec UMENOS
            | EXPRESION:a MAS EXPRESION:b   {:   RESULT = new Aritmeticas(a, b, OperadoresAritmeticos.SUMA, aleft, aright);   :}
            | EXPRESION:a MENOS EXPRESION:b {:   RESULT = new Aritmeticas(a, b, OperadoresAritmeticos.RESTA, aleft, aright);   :}
            | EXPRESION:a MULTIPLICACION EXPRESION:b {:   RESULT = new Aritmeticas(a, b, OperadoresAritmeticos.MULTIPLICACION, aleft, aright);   :}
            | EXPRESION:a DIVISION EXPRESION:b {:   RESULT = new Aritmeticas(a, b, OperadoresAritmeticos.DIVISION, aleft, aright);   :}
            | EXPRESION:a POTENCIA EXPRESION:b {:   RESULT = new Aritmeticas(a, b, OperadoresAritmeticos.POTENCIA, aleft, aright);   :}
            | EXPRESION:a MODULO EXPRESION:b {:   RESULT = new Aritmeticas(a, b, OperadoresAritmeticos.MODULO, aleft, aright);   :}
            | EXPRESION:a IGUALACION EXPRESION:b {:   RESULT = new Relacionales(a, b, OperadoresRelacionales.IGUALACION, aleft, aright); :}
            | EXPRESION:a MENORQUE EXPRESION:b {:   RESULT = new Relacionales(a, b, OperadoresRelacionales.MENORQUE, aleft, aright);   :}
            | EXPRESION:a MAYORQUE EXPRESION:b {:   RESULT = new Relacionales(a, b, OperadoresRelacionales.MAYORQUE, aleft, aright);   :}
            | EXPRESION:a MENOR EXPRESION:b {:   RESULT = new Relacionales(a, b, OperadoresRelacionales.MENOR, aleft, aright);   :}
            | EXPRESION:a MAYOR EXPRESION:b {:   RESULT = new Relacionales(a, b, OperadoresRelacionales.MAYOR, aleft, aright);   :}
            | EXPRESION:a DIFERENTE EXPRESION:b {:   RESULT = new Relacionales(a, b, OperadoresRelacionales.DIFERENTE, aleft, aright);   :}
            | EXPRESION:a OR EXPRESION:b {:   RESULT = new Logicos(a, b, OperadoresLogicos.OR, aleft, aright);   :}
            | EXPRESION:a AND EXPRESION:b {:   RESULT = new Logicos(a, b, OperadoresLogicos.AND, aleft, aright);   :}
            | EXPRESION:a XOR EXPRESION:b {:   RESULT = new Logicos(a, b, OperadoresLogicos.XOR, aleft, aright);   :}
            | LLAMADA:a {:   RESULT = a;   :}
            | NOT EXPRESION:a {:   RESULT = new Logicos(a, OperadoresLogicos.NOT, aleft, aright);   :}
            | PAR1 TIPOS:a PAR2 EXPRESION:b {:   RESULT = new Casteos(b, a, aleft, aright);   :}
            | ENTERO:a      {:   RESULT = new Nativo(Integer.parseInt(a), new Tipo(tipoDato.ENTERO), aleft, aright );   :}
            | DECIMAL:a     {:   RESULT = new Nativo(new Double(a), new Tipo(tipoDato.DECIMAL), aleft, aright );   :}
            | CADENA:a      {:   RESULT = new Nativo(a, new Tipo(tipoDato.CADENA), aleft, aright );   :}
            | BOOLEANO:a    {:  RESULT = new Nativo(Boolean.parseBoolean(a), new Tipo(tipoDato.BOOLEANO), aleft, aright );   :}
            | CARACTER:a    {:  RESULT = new Nativo(a, new Tipo(tipoDato.CARACTER), aleft, aright );   :}
            | ID:a          {:   RESULT= new AccesoVar(a, aleft,aright);   :}
            | ID:a CORCHE1 EXPRESION:b CORCHE2 {: RESULT = new AccesoVec(a, b,null, aleft, aright);   :}
            | ID:a CORCHE1 EXPRESION:b CORCHE2 CORCHE1 EXPRESION:c CORCHE2 {: RESULT = new AccesoVec(a, b, c, aleft, aright);   :}
            | PAR1 EXPRESION:a PAR2 {:   RESULT = a;   :}
;